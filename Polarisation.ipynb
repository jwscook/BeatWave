{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "4f37e09a",
   "metadata": {},
   "source": [
    "Following Felix's online [notes](http://www-thphys.physics.ox.ac.uk/people/FelixParra/CollisionlessPlasmaPhysics/notes/lecV_coldplasma.pdf), find we want to find two x-modes waves, one ion cyclotron frequency apart, that both fit an integer number of wavelengths into a periodic simulation domain.\n",
    "\n",
    "The dispersion relation $D(\\omega_{choose}, k_{find})=0$ must be solved to find $k$, or conversly by fixing $k$ and finding $\\omega$. In order to find a pair of waves that both fit in the box as described one must fine tune the parameters. In this notebook, I fine tune the density and keep the magnetic field fixed.\n",
    "\n",
    "The polarisation is calculated via $\\vec{\\vec{D}}(\\omega, k)\\cdot \\vec E=\\vec0$ where we find the nullspace of $\\vec{\\vec{D}}(\\omega, k)$ to give $\\vec E$ and hence relative amplitudes of the components of the electric field.\n",
    "\n",
    "For the extraordinary mode\n",
    "$$\n",
    "n^2=\\frac{\\epsilon_\\bot^2-g^2}{\\epsilon_\\bot}\\\\\n",
    "E_x\\propto i g\\\\\n",
    "E_y\\propto -\\epsilon_\\bot\\\\\n",
    "E_z = 0\\\\\n",
    "$$\n",
    "where $n=kc/\\omega$\n",
    "$$\n",
    "\\epsilon_\\bot = 1- \\sum_s \\frac{\\Pi_s^2}{\\omega^2 - \\Omega_s^2},\\\\\n",
    "g = -\\sum_s \\frac{\\Pi_s^2\\Omega_s}{\\omega (\\omega^2 - \\Omega_s^2)}\n",
    "$$\n",
    "\n",
    "It is necessary to fix some parameters and find others. One method is to fix the wavenumbers of the two modes and the difference in frequency. That way we can ensure that both modes fit exactly into a periodic simulation domain and the difference in their frequency will be one ion cyclotron frequency.\n",
    "\n",
    "Hence fix $k_\\alpha =n_{ka} 2\\pi/L$, $k_\\beta=n_{kb} 2\\pi/L$, and $\\omega_\\beta - \\omega_\\alpha = \\Omega_i$. \n",
    "\n",
    "The procedure is:\n",
    " - Fix $k_\\alpha$, $k_\\beta$\n",
    " - Optimise $n_e$ such that the dispersion relation is satisfied for two modes $\\alpha$ and $\\beta$ where $\\omega_\\beta = \\omega_\\alpha + \\Omega_i$.\n",
    " - Use the nullspace, or vector associated with the singular value, of the dispersion relation matrix to obtain the electric field polarisation for each of the waves.\n",
    " - Insert the electric field into Faraday's law for the magnetic field polarisation\n",
    " - Obtain the current polarisation from the conductivity tensor\n",
    " - Obtain the velocity perturbation for each species from their currents.\n",
    " - Obtain the density perturbation for each species from the continuity equation.\n",
    "\n",
    "\n",
    "Now the other parameters:\n",
    "\n",
    "Faraday's\n",
    "$$\n",
    "-i\\omega\\vec B_1 = - \\vec k \\times \\vec E_1\n",
    "$$\n",
    "Gauss's law\n",
    "$$\n",
    "i\\vec k \\cdot \\vec E_1 =  \\sum_s\\frac{q_s n_{s,1}}{\\epsilon_0}\\\\\n",
    "$$\n",
    "Ampere's law:\n",
    "$$\n",
    "\\mu_0 \\vec J = i \\vec k \\times \\vec {B}_1 + i \\omega \\mu_0 \\epsilon_0 \\vec E_1\\\\\n",
    "\\sum_s q_s n_{s,0} \\vec {v}_{s,1} = \\frac{i \\vec k \\times \\vec {B}_1}{\\mu_0} + i \\omega \\epsilon_0 \\vec E_1\\\\\n",
    "$$\n",
    "Assume the ions are unperturbed in the initial conditions\n",
    "$$\n",
    "q_e n_{e,0} {v}_{e,1,y} = i  \\omega \\epsilon_0 E_{1,x}\\\\\n",
    "q_e n_{e,0} {v}_{e,1,y} = -\\frac{i k {B}_{1,z}}{\\mu_0} + i \\omega \\epsilon_0 E_{1,y}\\\\\n",
    "q_e n_{e,0} {v}_{e,1,z} = \\frac{i k {B}_{1,y}}{\\mu_0} + i \\omega \\epsilon_0 E_{1,z}\\\\\n",
    "$$\n",
    "\n",
    "$$\n",
    "n_{e,1} = i k E_{x,1}\\frac{\\epsilon_0}{q_s}\n",
    "$$\n",
    "\n",
    "Continuity\n",
    "\n",
    "$$\n",
    "\\omega n_{1,s} - n_{0,s} \\vec k \\cdot \\vec v_{1,s} = 0\n",
    "$$\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "0f58c4e6",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "fn (generic function with 3 methods)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Roots, Plots, LinearAlgebra\n",
    "\n",
    "const c₀ = 2.99792458e8\n",
    "const ϵ₀ = 8.854187817e-12\n",
    "const μ₀ = 1.25663706144e-6\n",
    "const q₀ = 1.602176487e-19\n",
    "const mₑ = 9.10938188e-31\n",
    "const n0 = 1e19\n",
    "const B0 = 2.0\n",
    "fΠ(n, m::Float64=mₑ) = √(q₀^2 * n / m / ϵ₀)\n",
    "const Π0 = fΠ(n0)\n",
    "fΩ(m=mₑ, Z::Int=-1) = Z * q₀ * B0 / m\n",
    "const Ωe = fΩ(mₑ, -1)\n",
    "const mi = 1836 * mₑ * 2\n",
    "const Ωi = fΩ(mi, 1)\n",
    "fωL(n) = sqrt(Ωe^2 / 4 + fΠ(n)^2) + Ωe / 2\n",
    "const ωL0 = fωL(n0)\n",
    "fωₕ(n) = √(fΠ(n)^2 + Ωe^2)\n",
    "const ωₕ0 = √(fΠ(n0)^2 + Ωe^2)\n",
    "const Te_eV = 10e3\n",
    "const Ti_eV = 10e3\n",
    "const vth = sqrt(2 * 10e3 * q₀ / mₑ)\n",
    "fλD(n) = vth / fΠ(n)\n",
    "const λD0 = fλD(n0)\n",
    "const fbulkenergydensity(n) = B0^2 / 2μ₀ + n * Te_eV * q₀ + n * Ti_eV * q₀\n",
    "\n",
    "function cleaner(z)\n",
    "  nz = norm(z)\n",
    "  normz = z ./ nz\n",
    "  normz = trunc.(real.(normz), digits=16) .+ im * trunc.(imag.(normz), digits=16)\n",
    "  output = normz .* nz\n",
    "  @assert output ≈ z\n",
    "  return output\n",
    "end\n",
    "\n",
    "function fσ(ω, n, m=mₑ, Z=-1)\n",
    "  Ωs = fΩ(m, Z) \n",
    "  Πs = fΠ(n, m)\n",
    "  ω²_Ωs² = (ω^2 - Ωs^2)\n",
    "  return [ω / ω²_Ωs²        im * Ωs / ω²_Ωs²  0.0; \n",
    "          -im * Ωs / ω²_Ωs²  ω / ω²_Ωs²         0.0;\n",
    "          0.0               0.0                1 / ω] .* im * ϵ₀ * Πs^2\n",
    "end\n",
    "fϵ(ω, n) = I(3) + im * (fσ(ω, n, mₑ, -1) + fσ(ω, n, mi, 1)) / (ω * ϵ₀)\n",
    "fK(k, θ=pi/2) = k * [sin(θ), 0, cos(θ)]\n",
    "function fΛ(ω, k, n, θ=pi/2)\n",
    "    N = fK(k, θ) * c₀ / ω\n",
    "    return N * N' .- dot(N, N) * I(3) .+ fϵ(ω, n)\n",
    "end\n",
    "function fE(ω, k, n, θ=pi/2)\n",
    "  Λ = fΛ(ω, k, n, θ)\n",
    "  _, sigma, V = svd(Λ)\n",
    "  @assert sigma[3] <= sqrt(eps())\n",
    "  E = V[:, 3]\n",
    "  @assert E ≈ nullspace(Λ)\n",
    "  @assert all(abs.(Λ * E) .<= sqrt(eps()))\n",
    "  return E\n",
    "end\n",
    "realdetΛ(ω, k, n=n0) = real(det(fΛ(ω, k, n)))\n",
    "\n",
    "fB(ω, k, n) = cross([k, 0, 0], fE(ω, k, n)) / ω\n",
    "fJ(ω, k, n, m::Float64, Z::Int) = fσ(ω, n, m, Z) * fE(ω, k, n)\n",
    "fJ(ω, k, n) = fJ(ω, k, n, mₑ, -1) + fJ(ω, k, n, mi, 1)\n",
    "fV(ω, k, n, m::Float64=mₑ, Z::Int=-1) = fJ(ω, k, n, m, Z) / q₀ / n / Z\n",
    "fn(ω, k, n, m::Float64=mₑ, Z::Int=-1) = n * k * fV(ω, k, n, m, Z)[1] / ω\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "87c8d262",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(n1, n01, Ωe / fΠ(n01)) = (9.719738576360173, 9.719738576360173e19, -0.6324593453790026)\n"
     ]
    }
   ],
   "source": [
    "vthi = sqrt(Te_eV * q₀ * 2 / mi)\n",
    "rli = vthi / Ωi\n",
    "vthe = sqrt(Te_eV * q₀ * 2 / mₑ)\n",
    "rle = vthe / abs(Ωe)\n",
    "\n",
    "NG = 8 * 1024\n",
    "dx = min(λD0, rle)\n",
    "Lx = NG * dx\n",
    "Kl = 2π / Lx\n",
    "nka = 32\n",
    "nkb = nka + 1\n",
    "ka = nka * Kl\n",
    "kb = nkb * Kl\n",
    "@assert ka > 0\n",
    "@assert kb > 0\n",
    "function findn(n1)\n",
    "  n1 <= 0 && return Inf\n",
    "  ωa_Π0 = Roots.fzero(ω->realdetΛ(ω * Π0, ka, n1[1] * n0), 2, fatol=eps())\n",
    "  ωa = ωa_Π0 * Π0\n",
    "  ωtarget = ωa + Ωi\n",
    "  return realdetΛ(ωtarget, kb, n1[1] * n0)\n",
    "end\n",
    "\n",
    "n1 = Roots.fzero(findn, 10, xatol=1e-12)\n",
    "n01 = n1 * n0\n",
    "@show n1, n01, Ωe / fΠ(n01)\n",
    "ωstart = fΠ(n01) * 2\n",
    "\n",
    "ωa = Roots.fzero(ω->realdetΛ(ω * Π0, nka * Kl, n01), 2, fatol=eps()) * Π0\n",
    "@assert ωa > 0\n",
    "kb = nkb * Kl\n",
    "ωb = ωa + Ωi\n",
    "# assert that (ωa, ka) and (ωb, kb) are in fact solutions\n",
    "@assert isapprox(ωa + Ωi, ωb, atol=1e-12)\n",
    "@assert isapprox(ka, nka * Kl, atol=1e-12)\n",
    "@assert isapprox(kb, nkb * Kl, atol=1e-12)\n",
    "# @assert isapprox(realdetΛ(ωa, ka, n01), 0.0, atol=1e-14) \"realdetΛ(ωa, ka, n01) = $(realdetΛ(ωa, ka, n01))\"\n",
    "# @assert isapprox(realdetΛ(ωb, kb, n01), 0.0, atol=1e-14) \"realdetΛ(ωb, kb, n01) = $(realdetΛ(ωb, kb, n01))\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "be9ae429",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "pa = (E = ComplexF64[0.7074366208952682 - 0.0im, 0.0 - 0.7067767875477269im, 0.0 + 0.0im], B = ComplexF64[0.0 + 0.0im, -0.0 - 0.0im, 0.0 - 2.514894354722477e-10im], J = ComplexF64[0.0 + 2.562503237592256im, 2.530980815614421 + 0.0im, 0.0 + 0.0im], ve = ComplexF64[-0.0 - 0.16446775790847648im, -0.16244362945961724 - 0.0im, 0.0 - 0.0im], ne = -0.0 - 5.688173366704054e9im, vi = ComplexF64[0.0 + 8.284756880298837e-5im, 8.277033219476767e-5 + 0.0im, 0.0 + 0.0im], ni = 0.0 + 2.8653113555763527e6im)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "(E = ComplexF64[0.7074366208952682 - 0.0im, 0.0 - 0.7067767875477269im, 0.0 + 0.0im], B = ComplexF64[0.0 + 0.0im, -0.0 - 0.0im, 0.0 - 2.514894354722477e-10im], J = ComplexF64[0.0 + 2.562503237592256im, 2.530980815614421 + 0.0im, 0.0 + 0.0im], ve = ComplexF64[-0.0 - 0.16446775790847648im, -0.16244362945961724 - 0.0im, 0.0 - 0.0im], ne = -0.0 - 5.688173366704054e9im, vi = ComplexF64[0.0 + 8.284756880298837e-5im, 8.277033219476767e-5 + 0.0im, 0.0 + 0.0im], ni = 0.0 + 2.8653113555763527e6im)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function findpolarisation(ω, k, n)\n",
    "  Ep = cleaner(fE(ω, k, n))\n",
    "  Bp = cleaner(fB(ω, k, n))\n",
    "  Jp = cleaner(fJ(ω, k, n))\n",
    "  ve = cleaner(fV(ω, k, n, mₑ, -1))\n",
    "  ne = cleaner(fn(ω, k, n, mₑ, -1))\n",
    "  vi = cleaner(fV(ω, k, n, mi, 1))\n",
    "  ni = cleaner(fn(ω, k, n, mi, 1))\n",
    "  return (E=Ep, B=Bp, J=Jp, ve=ve, ne=ne, vi=vi, ni=ni)\n",
    "end\n",
    "pa = findpolarisation(ωa, ka, n01)\n",
    "pb = findpolarisation(ωb, kb, n01)\n",
    "function checkpolarisation(ω, k, polarisation)\n",
    "  E = polarisation[:E]\n",
    "  B = polarisation[:B]\n",
    "  J = polarisation[:J]\n",
    "  ve = polarisation[:ve]\n",
    "  vi = polarisation[:vi]\n",
    "  ne = polarisation[:ne]\n",
    "  ni = polarisation[:ni]\n",
    "  @assert isapprox(B[1], 0, atol=eps()) # Faraday x & no magnetic monopoles\n",
    "  @assert isapprox(E[3], 0, atol=eps()) # Faraday x, polarisation of x-mode\n",
    "  @assert all(ω * B ≈ cross([k, 0, 0], E)) # Faraday\n",
    "  @assert ω * B[2] ≈ -k * E[3] # Faraday y, trivially zero\n",
    "  @assert ω * B[3] ≈ k * E[2] # Faraday z\n",
    "  @assert all(-im * J ≈ cross([k,0,0], B) / μ₀ + ω * ϵ₀ * E) # Ampere\n",
    "  @assert μ₀ * J[1] ≈ im * ω * μ₀ * ϵ₀ * E[1] # Ampere x\n",
    "  @assert μ₀ * J[2] ≈ - im * k * B[3] + im * ω * μ₀ * ϵ₀ * E[2] # Ampere y\n",
    "  @assert isapprox(μ₀ * J[3], im * k * B[2] + im * ω * μ₀ * ϵ₀ * E[3], atol=1e-16) # Ampere z, trivially zero\n",
    "  @assert im * k * E[1] * ϵ₀ ≈ q₀ * (ni - ne) # Gauss\n",
    "  @assert im * k * J[1] ≈ im * q₀ * (ni - ne) * ω # divJ + drho/dt = 0 \n",
    "  @assert all(J ≈ q₀ * n01 * (vi - ve)) # current\n",
    "  @assert J[1] ≈ q₀ * n01 * (vi - ve)[1] # current x\n",
    "  @assert J[2] ≈ q₀ * n01 * (vi - ve)[2] # current y\n",
    "  @assert J[3] ≈ q₀ * n01 * (vi - ve)[3] # current z, trivially zero\n",
    "end\n",
    "checkpolarisation(ωa, ka, pa)\n",
    "checkpolarisation(ωb, kb, pb)\n",
    "@show pa\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "5cc76aa9",
   "metadata": {},
   "outputs": [],
   "source": [
    "function scale(polarisation, factor::Real)\n",
    "  return NamedTuple(k=>factor * v for (k, v) in pairs(polarisation))\n",
    "end\n",
    "function zeroions(polarisation)\n",
    "  return NamedTuple(k=>!(k in (:vi, :ni)) * v for (k, v) in pairs(polarisation))\n",
    "end\n",
    "function energy(polarisation)\n",
    "  E = polarisation[:E]\n",
    "  B = polarisation[:B]\n",
    "  ve = polarisation[:ve]\n",
    "  vi = polarisation[:vi]\n",
    "  return real(ϵ₀ * dot(E,E) / 2 + dot(B,B) / 2μ₀ +\n",
    "            mₑ * n01 * dot(ve,ve) / 2 + mi * n01 * dot(vi,vi) / 2)\n",
    "end\n",
    "printer(polarisation, i) = printer(stdout, polarisation, i)\n",
    "\n",
    "function printcommon(io)\n",
    "  println(io, \"n0 = $n01\")\n",
    "  println(io, \"B0 = $B0\")\n",
    "  println(io, \"Lx = $Lx\")\n",
    "  println(io, \"NG = $NG\")        \n",
    "  println(io, \"Te_eV = $Te_eV\")\n",
    "  println(io, \"Ti_eV = $Ti_eV\")\n",
    "  println(io, \"mioverme = $(round(Int, mi / mₑ))\")\n",
    "end\n",
    "function printscalar(io, scalar, str)\n",
    "  println(io, str * \"_cos = $(real(scalar))\")\n",
    "  # Minus sign because real((a + im*b) * exp(im *k*x)) = a*cos(k*1x) - b*sin(k*x).\n",
    "  println(io, str * \"_sin = $(-imag(scalar))\")\n",
    "end\n",
    "function printvector(io, vec, str)\n",
    "  components = [\"x\", \"y\", \"z\"]\n",
    "  for i in 1:3\n",
    "    printscalar(io, vec[i], str * components[i])\n",
    "  end\n",
    "end\n",
    "\n",
    "function printer(io, polarisation, i)\n",
    "  printvector(io, polarisation[:E], \"E$(i)\")\n",
    "  printvector(io, polarisation[:B], \"B$(i)\")\n",
    "  printvector(io, polarisation[:J], \"J$(i)\")\n",
    "  printvector(io, polarisation[:ve] * mₑ, \"Pe$(i)\")\n",
    "  printvector(io, polarisation[:vi] * mi, \"Pi$(i)\")\n",
    "  printscalar(io, polarisation[:ne], \"ne$(i)x\")\n",
    "  printscalar(io, polarisation[:ni], \"ni$(i)x\")\n",
    "end\n",
    "bulkenergydensity = fbulkenergydensity(n01)\n",
    "energyratio = 1e-2\n",
    "pb = scale(pb, sqrt(energy(pa) / energy(pb)))\n",
    "@assert energy(pa) ≈ energy(pb)\n",
    "pa = scale(pa, sqrt(bulkenergydensity * energyratio / energy(pa)))\n",
    "pb = scale(pb, sqrt(bulkenergydensity * energyratio / energy(pb)))\n",
    "@assert energy(pa) ≈ energyratio * bulkenergydensity\n",
    "@assert energy(pb) ≈ energyratio * bulkenergydensity\n",
    "checkpolarisation(ωa, ka, pa) # make sure they're still consitent\n",
    "checkpolarisation(ωb, kb, pb) # make sure they're still consitent\n",
    "\n",
    "open(\"null.prefix.deck\",\"w\") do io\n",
    "  println(io, \"begin:constant\")\n",
    "  printcommon(io)\n",
    "  println(io, \"# Wave A\")\n",
    "  println(io, \"w1 = $ωa\")\n",
    "  println(io, \"k1 = $ka\")\n",
    "  printer(io, scale(pa, 0), 1)\n",
    "  println(io, \"# Wave B\")\n",
    "  println(io, \"w2 = $ωb\")\n",
    "  println(io, \"k2 = $kb\")\n",
    "  printer(io, scale(pb, 0), 2)\n",
    "end\n",
    "\n",
    "open(\"wave1.prefix.deck\",\"w\") do io\n",
    "  println(io, \"begin:constant\")\n",
    "  printcommon(io)\n",
    "  println(io, \"# Wave A\")\n",
    "  println(io, \"w1 = $ωa\")\n",
    "  println(io, \"k1 = $ka\")\n",
    "  printer(io, pa, 1)\n",
    "  println(io, \"# Wave B\")\n",
    "  println(io, \"w2 = $ωb\")\n",
    "  println(io, \"k2 = $kb\")\n",
    "  printer(io, scale(pb, 0), 2)\n",
    "end\n",
    "\n",
    "open(\"wave2.prefix.deck\",\"w\") do io\n",
    "  println(io, \"begin:constant\")\n",
    "  printcommon(io)\n",
    "  println(io, \"# Wave A\")\n",
    "  println(io, \"w1 = $ωa\")\n",
    "  println(io, \"k1 = $ka\")\n",
    "  printer(io, scale(pa, 0), 1)\n",
    "  println(io, \"# Wave B\")\n",
    "  println(io, \"w2 = $ωb\")\n",
    "  println(io, \"k2 = $kb\")\n",
    "  printer(io, pb, 2)\n",
    "end\n",
    "\n",
    "open(\"both.prefix.deck\",\"w\") do io\n",
    "  println(io, \"begin:constant\")\n",
    "  printcommon(io)\n",
    "  println(io, \"# Wave A\")\n",
    "  println(io, \"w1 = $ωa\")\n",
    "  println(io, \"k1 = $ka\")\n",
    "  printer(io, pa, 1)\n",
    "  println(io, \"# Wave B\")\n",
    "  println(io, \"w2 = $ωb\")\n",
    "  println(io, \"k2 = $kb\")\n",
    "  printer(io, pb, 2)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "ff0d6daa",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "ceil((0.01 * ((2π) / Ωi)) / ((2π) / ωa)) = 43.0\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "43.0"
      ]
     },
     "execution_count": 11,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "ωs = range(fωL(n01), stop=0.99fωₕ(n01), length=1024);\n",
    "ks = [try;Roots.fzero(k->realdetΛ(ω, k, n01), 10abs(Ωe)/c₀);catch;missing;end for ω in ωs];\n",
    "\n",
    "plot(ks .* c₀ / fΠ(n01), ωs ./ fΠ(n01))\n",
    "plot!([1, 1, 0, 1] .* ka * c₀ / fΠ(n01), [0, 1, 1, 1] .* ωa / fΠ(n01))\n",
    "plot!([1, 1, 0, 1] .* kb * c₀ / fΠ(n01), [0, 1, 1, 1] .* ωb / fΠ(n01))\n",
    "xlabel!(\"Wavenumber [Π / c]\")\n",
    "xlabel!(\"Frequency [Π]\")\n",
    "@show ceil(0.01 * (2π/Ωi) / (2π/ωa))\n"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": "3eaf98e13948451e97f316806ca6c0be",
   "lastKernelId": "8dc15439-d5a9-4d67-87ba-cd06ec2f286f"
  },
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
